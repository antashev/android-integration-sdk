plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
}

Properties localProperties = new Properties()
File propertiesFile = project.rootProject.file('local.properties')
if (propertiesFile.exists()) {
    localProperties.load(propertiesFile.newDataInputStream())
}

android {
    compileSdkVersion 31

    defaultConfig {
        applicationId "ru.modulkassa.pos.integration.demo"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 3
        versionName "1.2"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    // For Kotlin projects
    kotlinOptions {
        jvmTarget = "11"
    }

    lintOptions {
        abortOnError false
    }

    signingConfigs {
        release {
            storeFile file("../demo_keystore.jks")
            storePassword System.getProperty("android.storePassword") ?: localProperties.getProperty("android.storePassword")
            keyAlias System.getProperty("android.keyAlias") ?: localProperties.getProperty("android.keyAlias")
            keyPassword System.getProperty("android.keyPassword") ?: localProperties.getProperty("android.keyPassword")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    implementation project(':integration-library')
    implementation libraries.kotlin
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
}
